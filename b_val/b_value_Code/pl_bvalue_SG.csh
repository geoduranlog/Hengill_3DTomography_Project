#! /bin/csh

# Plots b-value generated by f77 code bvalue.f
# T. Diehl ETHZ, 2011-05-23
#
# Usage: pl_bvalue.csh <b-value file> <Title>
# e.g. : pl_bvalue.csh Magnitudes_Swarm_ISC_08.txt.NEIC.mb Swarm
#



if(($1 == -h) || ($1 == -H) || ($#argv != 2)) then
   head -8 $0 | tail -7  | more
   exit
endif

#File frequency of magnitude over magnitude:
#set magfi = /Users/tdiehl/Documents/Sumatra_Docu/Paper_Swarm/GMT_Scripts/Magnitudes_Swarm_ISC.txt.NEIC.mb
#set magfi = /Users/tdiehl/Documents/Sumatra_Docu/Paper_Swarm/GMT_Scripts/Magnitudes_Swarm_ISC_14.txt.NEIC.mb
#set ofile = /Users/tdiehl/Documents/Sumatra_Docu/Paper_Swarm/GMT_Scripts/pl_bvalue_mb_NEIC.ps

 set magfi = $1
 set ofile = $1".ps"
 set title = $2

if(! -e $magfi) then
   echo "ERROR: File not found: $magfi"
   exit
endif

#Range:
 set magmin = -1.5
 set magmax = 4.0
 set nobmin = 0.50
 set nobmax = 1000

#Annotations:
 set ax = a0.5f0.1
 set ay = a1f0.1

##############################################################
#Media (A4 or letter)
gmtset PAPER_MEDIA letter

#Chose page orientation : portrait/landscape
#set format=landscape
set format=portrait
gmtset PAGE_ORIENTATION $format

#Maps:
gmtset BASEMAP_TYPE plain
gmtset OUTPUT_DEGREE_FORMAT +D
gmtset PLOT_DEGREE_FORMAT +DF

#Choose measure unit (inch or cm):
gmtset MEASURE_UNIT cm

#Label Font Size:
gmtset LABEL_FONT_SIZE 09

#Annotation Font Size:
gmtset ANOT_FONT_SIZE 08

#Offset of label:
gmtset LABEL_OFFSET 0.2c

##############################################################

set xscale = 15/10l
set xj   = X"$xscale"
set xrlp = $magmin/$magmax/$nobmin/$nobmax

#Check input format:
set format = `grep "# SUMMARY:" $magfi | awk 'BEGIN{fo="for"}{if(NF>8){fo="mat"}}END{print fo}'`
echo "File Format: "$format

#Open figure:
psbasemap -R$xrlp -J$xj -B"$ax":"Magnitude":/"$ay":"Number":nSeW -Y+6.35 -K > $ofile

#Check if summary line exists:
set ck1 = `grep -c "# SUMMARY: " $magfi | awk '{print $1}'`

if($ck1 == 1 && $format == "mat") then
   set mbin = `grep "# SUMMARY: " $magfi | awk '{print $9}'`
   awk '{t1=substr($0,1,6);if(t1=="# MAG:"){print $3}}' $magfi | pshistogram -R -J$xj -K -O -F -W$mbin -G200 -L1/0 >> $ofile
   awk '{t1=substr($0,1,1);if(t1!="#")  {print $1,$3}}' $magfi | psxy -R -J$xj -K -O -Sx0.10 -G0 -W1/0 >> $ofile
endif

awk '{t1=substr($0,1,1);if(t1!="#")     {print $1,$2}}' $magfi | psxy -R -J$xj -K -O -Sc0.30 -G75 -W1/0 >> $ofile
awk '{t1=substr($0,1,6);if(t1=="# SLF:"){print $3,$4}}' $magfi | psxy -R -J$xj -K -O              -W3/0 >> $ofile

#Check if summary line exists:
set ck1 = `grep -c "# SUMMARY: " $magfi | awk '{print $1}'`

if($ck1 == 1) then
   set agency = `grep "# SUMMARY: " $magfi | awk '{print $3}'`
   set magtyp = `grep "# SUMMARY: " $magfi | awk '{print $4}'`
   set magcom = `grep "# SUMMARY: " $magfi | awk '{print $5}'`
   set magobs = `grep "# SUMMARY: " $magfi | awk '{print $6}'`
   set bvalue = `grep "# SUMMARY: " $magfi | awk '{print $7}'`
   set bvaler = `grep "# SUMMARY: " $magfi | awk '{print $8}'`

   echo $title                  | awk '{print "2.5 0400 12 0 1 LM "$1}' | pstext -R -J$xj -K -O -N >> $ofile
   echo $agency $magtyp $magcom | awk '{print "2.5 0200 12 0 0 LM",$1,$2," Mc="$3}' | pstext -R -J$xj -K -O -N >> $ofile
   echo $magobs                 | awk '{print "2.5 0100 12 0 0 LM #Obs: ",$1}'      | pstext -R -J$xj -K -O -N >> $ofile
   echo $bvalue $bvaler         | awk '{print "2.5 0050 12 0 0 LM b-value: ",$1,"+/-",$2}' | pstext -R -J$xj -K -O -N >> $ofile
endif


#Close plot:
psxy -J$xj -R -O < /dev/null >> $ofile

gv $ofile &

